apply plugin: 'com.android.application'
apply plugin: 'com.github.triplet.play'
def homePath = System.properties['user.home']
android {
    compileSdkVersion 25
    buildToolsVersion '24.0.3'

    defaultConfig {
        applicationId "com.ichi2.anki"
        minSdkVersion 10
        targetSdkVersion 25
        testApplicationId "com.ichi2.anki.tests"
        vectorDrawables.useSupportLibrary = true
    }
    lintOptions {
        abortOnError false
        disable 'TypographyEllipsis'
    }
    signingConfigs {
        release {
            storeFile file("${homePath}/src/android-keystore")
            keyAlias "nrkeystorealias"
            storePassword System.getenv("KSTOREPWD")
            keyPassword System.getenv("KEYPWD")
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    useLibrary 'org.apache.http.legacy'

    dexOptions {
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !travisBuild
    }
}


play {
    serviceAccountEmail = '294046724212-r3bef6kl46pb9gk0h1pl5rcjmpfrdpjl@developer.gserviceaccount.com'
    pk12File = file("${homePath}/src/583631bdd16d.p12")
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:25.0.0'
    // Note: the design support library can be quite buggy, so test everything thoroughly before updating it
    compile 'com.android.support:design:25.0.0'
    compile 'com.android.support:customtabs:25.0.0'
    compile 'com.android.support:recyclerview-v7:25.0.0'
    compile 'io.requery:sqlite-android:3.15.0'
    compile('com.afollestad.material-dialogs:core:0.8.6.2@aar') {
        //exclude group: 'com.android.support'  // uncomment to force our local support lib version
        transitive = true
    }
    compile 'com.getbase:floatingactionbutton:1.10.1'
    compile 'ch.acra:acra:4.6.2'
    compile 'com.jakewharton.timber:timber:2.7.1'
    compile 'com.google.code.gson:gson:2.4'
    compile project(":api")
}

apply from: "testconfig.gradle"
