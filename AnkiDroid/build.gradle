apply plugin: 'com.android.application'
apply plugin: 'com.github.triplet.play'
def homePath = System.properties['user.home']
android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        applicationId "com.ichi2.anki"
        minSdkVersion 10
        targetSdkVersion 23
        testApplicationId "com.ichi2.anki.tests"
    }
    lintOptions {
        abortOnError false
        disable 'TypographyEllipsis'
    }
    signingConfigs {
        release {
            storeFile file("${homePath}/src/android-keystore")
            keyAlias "nrkeystorealias"
            storePassword System.getenv("KSTOREPWD")
            keyPassword System.getenv("KEYPWD")
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}


play {
    serviceAccountEmail = '294046724212-r3bef6kl46pb9gk0h1pl5rcjmpfrdpjl@developer.gserviceaccount.com'
    pk12File = file("${homePath}/src/583631bdd16d.p12")
    track = 'beta'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    // Note: support library 23.1.0 causes a crash opening prefs: https://github.com/ankidroid/Anki-Android/issues/3739
    compile 'com.android.support:appcompat-v7:23.0.1'
    // Note: the design support library can be quite buggy, so test everything thoroughly before updating it
    compile 'com.android.support:design:23.0.1'
    compile 'com.android.support:customtabs:23.0.1'
    compile 'com.android.support:recyclerview-v7:23.0.1'
    compile('com.afollestad.material-dialogs:core:0.8.5.0@aar') {
        transitive = true
        exclude group: 'com.android.support'
    }
    compile('com.mikepenz:materialdrawer:4.4.3@aar') {
        exclude group: 'com.android.support'
        transitive = true
    }
    compile 'net.i2p.android.ext:floatingactionbutton:1.10.0'
    compile 'ch.acra:acra:4.6.2'
    compile 'com.jakewharton.timber:timber:2.7.1'
    compile 'com.google.code.gson:gson:2.4'
    compile "cz.msebera.android:httpclient:4.4.1.1"
    compile project(":api")
}
